apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'


sourceCompatibility = 1.5

group 'com.mageddo'
archivesBaseName = 'spring-profile'
version '1.0.0'

repositories {
	mavenCentral()
}


configurations {
	provided
}

sourceSets {
	main {
		compileClasspath += configurations.provided
		test.compileClasspath += configurations.provided
		test.runtimeClasspath += configurations.provided
	}
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

dependencies {

	provided group: 'org.springframework', name: 'spring-core', version: springVersion
	provided group: 'commons-logging', name: 'commons-logging', version: '1.2'

	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name 'Spring Profile Env'
				packaging 'jar'
				// optionally artifactId can be defined here
				description 'Use Spring Env Profile without spring boot, only using spring core'
				url 'https://github.com/mageddo/spring-projects/tree/master/spring-profile'

				scm {
					connection 'https://github.com/mageddo/spring-projects.git'
					developerConnection 'https://github.com/mageddo/spring-projects.git'
					url 'https://github.com/mageddo/spring-projects/tree/master/spring-profile'
				}

				licenses {
					license {
						name 'The Apache License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id 'mageddo'
						name 'Elvis Souza'
						email 'edigitalb@gmail.com'
					}
				}
			}
		}
	}
}