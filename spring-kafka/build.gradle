apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group 'com.mageddo'
archivesBaseName = 'spring-kafka'
version '1.2.0'

repositories {
	mavenCentral()
}

configurations {
	provided
}

sourceSets {
	main {
		compileClasspath += configurations.provided
		test.compileClasspath += configurations.provided
		test.runtimeClasspath += configurations.provided
	}
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

dependencies {

	provided group: 'org.springframework.boot', name: 'spring-boot-starter', version: springBootVersion
	provided group: 'org.springframework.kafka', name: 'spring-kafka', version: springKafkaVersion
	provided group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.5'


	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
	testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion
	testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc', version: springBootVersion
	testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: springBootVersion
	testCompile group: 'org.hsqldb', name: 'hsqldb', version: '2.2.8'

}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name 'Spring Kafka'
				packaging 'jar'
				description 'Simplify Consumer And Producer Configuration'
				url 'https://github.com/mageddo/spring-projects/tree/master/spring-kafka'

				scm {
					connection 'https://github.com/mageddo/spring-projects.git'
					developerConnection 'https://github.com/mageddo/spring-projects.git'
					url 'https://github.com/mageddo/spring-projects/tree/master/spring-kafka'
				}

				licenses {
					license {
						name 'The Apache License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id 'mageddo'
						name 'Elvis Souza'
						email 'edigitalb@gmail.com'
					}
				}
			}
		}
	}
}